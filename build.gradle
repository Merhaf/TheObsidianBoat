import net.minecraftforge.gradle.common.util.ModConfig

buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'

    }
}

plugins {
    id "com.matthewprenger.cursegradle" version "1.4.0"
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'maven-publish'

apply from: 'https://raw.githubusercontent.com/MinecraftModDevelopment/Gradle-Collection/master/generic/markdown-git-changelog.gradle'
apply from: "https://raw.githubusercontent.com/UnRealDinnerbone/GradleHelpers/master/version.gradle"

archivesBaseName = project.archives_base_name
version = getVersionNumber()
group = 'com.unrealdinnerbone'

def ENV = System.getenv()


def mixinJson = project.mod_id + ".mixins.json"

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: 'official', version: '1.16.5'
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    ModConfig projectMod = new ModConfig(project, project.getName());
    projectMod.sources(project.sourceSets.main)

    runs {
        client {
            workingDirectory project.file('run')
            mods.add(projectMod)
            args '--mixin.config', mixinJson
        }

        server {
            workingDirectory project.file('run')
            args '--mixin.config', mixinJson
            mods.add(projectMod)
        }

        data {
            workingDirectory project.file('data')
            args '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            mods.add(projectMod)
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }


mixin {
    add sourceSets.main, project.mod_id + '.refmap.json'
}

dependencies {
    minecraft 'net.minecraftforge:forge:' + project.minecraft_version + '-' + project.forge_version
    if (System.getProperty("idea.sync.active") != "true") {
        annotationProcessor 'org.spongepowered:mixin:0.8.2:processor'
    }
}

jar {
     manifest {
        attributes([
                "MixinConfigs": mixinJson,
                "Specification-Title"     : project.mod_id,
                "Specification-Vendor"    : "UnRealDinnerbone",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : getVersionNumber(),
                "Implementation-Vendor"   : "UnRealDinnerbone",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
        ])
    }
}

jar.finalizedBy('reobfJar')

task sourcesJar(type: Jar) {
    classifier = "sources"
    from sourceSets.main.allJava
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = archivesBaseName
            artifact(jar) {
                classifier null
            }
            artifact(sourcesJar) {
                builtBy sourcesJar
            }
        }
    }


    repositories {
        if (ENV.NANITE_DEPLOY) {
            maven {
                url "https://maven.nanite.dev/"
                credentials {
                    username = "nanite"
                    password = "${ENV.NANITE_DEPLOY}"
                }
            }
        }
    }
}

if (ENV.CURSE_DEPLOY) {
    curseforge {
        apiKey = ENV.CURSE_DEPLOY
        project {
            id = project.curseforge_id
            releaseType = project.release_type
            addGameVersion "Forge"
            addGameVersion project.minecraft_version
            mainArtifact(jar)
            changelog = getGitChangelog()
            changelogType = 'markdown'
        }
    }
}


//Changelog
//Curse Info
//println("Changelog: " + getGitChangelog())
//println("Release Type: " + project.property("release_type"))
